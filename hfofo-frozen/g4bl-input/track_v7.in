#  30 periods HFOFO + 1 period matching + exit solenoid

material GH2 Z=1 A=1.01 density=0.014 # 20% of LH2
param worldMaterial=GH2

param pi=3.1415926

physics QGSP_BIC 

param zshift=60
param period=4200 # HFOFO period

param beamstart=-700 # from the rotator solenoid exit at z=0
beam ascii file=initial.dat beamZ=$beamstart # user provided file with particle data
# beam gaussian particle=mu+ nEvents=100 beamZ=-700.0 \
    sigmaX=10.0 sigmaY=10.0 sigmaXp=0.100 sigmaYp=0.100 \
    meanMomentum=200.0 sigmaP=4.0 meanT=0.0 sigmaT=0.0

param beamtime=-0.671 # average time of mu+ in initial.dat modulo RF period TRF
# if there is no mu+ take mu- and add TRF/2

param toffs0=$beamtime-$beamstart/275.89-.07 # time needed to travel from beamZ to 0

param toffs1=$toffs0-.061-.0026+.0115+.006 # timing on the beam core done iteratively
param toffs2=$toffs1-.064+.149-.0052-.0029
param toffs3=$toffs2
param toffs4=$toffs3
param toffs5=$toffs4-.008
param toffs6=$toffs5

# Additional parameters
trackcuts keep=mu+,mu-,pi+,pi- kineticEnergyCut=50

param maxStep=10.0 steppingVerbose=0 nTrace=0

### detector

include detectors.txt
virtualdetector DetLast file=outlast format=ascii radius=300 color=0,1,0 length=0.001 material=Vacuum

### Mag Field


coil kat0 innerRadius=360 outerRadius=500 length=6000 nSheets=10 maxR=360 maxZ=9000 dR=20 dZ=20 \
tolerance=0.003 

solenoid RotSol coilName=kat0 current=11.37 kill=1 

coil kat11 innerRadius=420 outerRadius=600 length=300 nSheets=10 maxR=360 maxZ=6000 dR=15 dZ=15 \
tolerance=0.003 


param pitch=-0.0025*180/$pi
param BLS=21.4 delf=0
param cur0=4.421*$BLS
param curpl=$cur0*(1+$delf/2) curmn=-$cur0*(1-$delf/2) 
param cur1=4.421*10.98 cur2=-4.421*17.89 cur3=4.421*19.64 cur4=-4.421*21.12 # entrance
param cur5=4.421*10.06 cur6=-4.421*16.03 cur7=4.421*17.81 cur8=-4.421*19.17 cur9=4.421*19.20 # exit

include solangles1.dat
include solangles2.dat

solenoid SolPos coilName=kat11 current=$curpl kill=1 color=invisible # color=1,0,1  
solenoid SolNeg coilName=kat11 current=$curmn kill=1 color=invisible # color=0,0,1  


### collimators

tubs abtube length=160 innerRadius=500 outerRadius=550 material=Vacuum kill=1 color=invisible
tubs terminus length=1 innerRadius=0 outerRadius=360 material=Vacuum kill=1 color=invisible

### pressure wall

tubs presswall length=4 outerRadius=360 material=Stainless316 color=invisible

# LiH wedge absorber

trap wedge0 height=350 length=700 upperWidth=0.005 lowerWidth=59.5 material=LITHIUM_HYDRIDE color=0,0,1 

### define the pillbox RF cavity
param Grad=25
param Grad0=20

pillbox RFC0 innerLength=249 frequency=0.325 maxGradient=$Grad0 \
win1Thick=0 win1OuterRadius=0 collarThick=0 collarRadialThick=0\
winMat=Be irisRadius=300 win2Thick=0.12 wallThick=0.4 pipeThick=0.4 

pillbox RFC innerLength=249 frequency=0.325 maxGradient=$Grad \
win1Thick=0 win1OuterRadius=0 collarThick=0 collarRadialThick=0\
winMat=Be irisRadius=300 win2Thick=0.12 wallThick=0.4 pipeThick=0.4 cavityMaterial=GH2 kill=1

pillbox RFC1 innerLength=249 frequency=0.325 maxGradient=$Grad \
win1Thick=0 win1OuterRadius=0 collarThick=0 collarRadialThick=0\
winMat=Be irisRadius=250 win2Thick=0.1 wallThick=0.4 pipeThick=0.4 cavityMaterial=GH2 kill=1

pillbox RFC2 innerLength=249 frequency=0.325 maxGradient=$Grad \
win1Thick=0 win1OuterRadius=0 collarThick=0 collarRadialThick=0\
winMat=Be irisRadius=200 win2Thick=0.07 wallThick=0.4 pipeThick=0.4 cavityMaterial=GH2 kill=1

param np=31 # number of snake periods
param zlastRF=$np*$period+1450

include abs_place7_31.txt
include RFplace7_31.txt
include sol_place7_31.txt

place DetLast z=$zlastRF+100
place terminus z=-1000 # to stop reflected particles
place terminus z=$zlastRF+102

#zntuple zloop=4250:8450:10 format=ascii file=zntuple
zntuple zloop=4700:134250:100 format=for009.dat file=for009
