#################
### CONSTANTS ###
#################

# Reference momentum:
param ref_p=200.0

######################
### INITIALIZATION ###
######################

# Physics:
physics QGSP_BIC doStochastics=0

# Reference particle:
reference particle=neutron \
    beamX=0.0 beamY=0.0 beamZ=-3000 \
    beamXp=0.0 beamYp=0.0 referenceMomentum=$ref_p
trace nTrace=0 format=ascii

# Beam:
beam gaussian particle=mu+ nEvents=1 \
    beamX=1.5 beamY=0.0 beamZ=-3000 \
    beamXp=0.0 beamYp=0.0 \
    sigmaX=0.0 sigmaY=0.0 sigmaXp=0.0 sigmaYp=0.0 \
    meanMomentum=$ref_p sigmaP=0.0 \ 
    meanT=0.0 sigmaT=0.0

# Remove secondaries:
trackcuts killSecondaries=1

############################
### GEOMETRY DEFINITIONS ###
############################

# Constant-current HFOFO parameters:
param length=300
param inner_radius=360
param outer_radius=500
param current=80.46
param spacing=700

# Solenoid pitch (in degrees):
param pitch=0.8

# Maximum step size:
param maxStep=5.0

# Solenoids:
coil coil innerRadius=$inner_radius outerRadius=$outer_radius length=$length \
    maxR=5000 maxZ=10000
solenoid sol_pos coilName=coil current=$current
solenoid sol_neg coilName=coil current=-$current

################################
### ELEMENT PLACING SEQUENCE ###
################################

do i 0 2

    place sol_pos x=0 y=0 z=2*$spacing*$i rotation=X$pitch
    place sol_neg x=0 y=0 z=2*$spacing*$i+$spacing rotation=X$pitch

enddo